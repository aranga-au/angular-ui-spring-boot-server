//spring boot app
//copyright Aranga Nanayakkara
buildscript {
	ext {
		springBootVersion = '1.4.0.M1'
	}
	repositories {
		mavenLocal()
		mavenCentral()
		maven {
            url "https://repo.spring.io/snapshot"
        }
		maven {
            url "https://repo.spring.io/milestone"
        }
		maven {
            url "https://plugins.gradle.org/m2/"
        }
		
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("com.moowork.gradle:gradle-node-plugin:0.12")	
		classpath("com.moowork.gradle:gradle-gulp-plugin:0.12")
	}
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.gulp'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}



dependencies {
	//compile('org.springframework.cloud:spring-cloud-starter-config')
	//compile('org.springframework.cloud:spring-cloud-starter-ribbon')
	//compile('org.springframework.cloud:spring-cloud-starter-zuul')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.cloud:spring-cloud-starter-eureka')
	testCompile('org.springframework.boot:spring-boot-starter-test') 
}

dependencyManagement {
	imports { 
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Brixton.BUILD-SNAPSHOT" 
	}
}
sourceSets {
    main {
        resources {
            exclude '**/bower_components'
			exclude '**/tmp'
        }
    }
}

task buildAngular(type: GulpTask,dependsOn:npmInstall){
	args = ["build"]
}
task copyAngular(type: Copy,dependsOn:buildAngular) {
	from 'build/clientApp'
	into 'build/resources/main/client'
}


task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}

jar {
	baseName = 'ui-app'
    String tag =java.lang.System.getProperty("TAG")
    if (tag == null)
    {
        tag = '0.0.1'
    }
	version = tag
	exclude '**/app'
	exclude '**/*.less'
}
/*
springBoot {
	executable = true
}*/
//execute before jar creation
jar.dependsOn copyAngular